apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'distribution'

project.group = 'edu.berkeley.calnet.libs'
project.version = '0.9-SNAPSHOT'

targetCompatibility = 1.7

repositories {
    mavenLocal()
    maven {
        url 'https://maven.calnet.berkeley.edu/artifactory/all/'
        credentials {
            username calnet_deploy_user
            password calnet_deploy_password
        }
    }
}

def grailsVersion = "2.4.4"
dependencies {
    compile "org.codehaus.groovy:groovy-all:2.4.3"
    compile "org.grails:grails-core:$grailsVersion"
    testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
    testCompile "org.grails:grails-dependencies:$grailsVersion"
    testCompile "javax.servlet:javax.servlet-api:3.0.1"
    testCompile "org.grails:grails-plugin-testing:$grailsVersion"
    testCompile "org.grails:grails-datastore-test-support:1.0.2-grails-2.4"
    testCompile "org.grails.plugins:hibernate4:4.3.6.1"
}

jar {
    baseName = rootProject.name
    version = project.version
}

distributions {
    main {
        baseName = rootProject.name
        contents {
            from fileTree(dir: '.', includes: [
                    'build/libs/**',
                    'src/**',
                    'gradle/**',
                    'gradelw', 'gradlew.bat',
                    'build.gradle', 'settings.gradle', 'gradle.properties'
            ])
        }
    }
}
distZip.dependsOn jar
distTar.dependsOn jar

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: calnet_deploy_contextUrl + '/' + calnet_deploy_repo) {
                authentication(
                        userName: calnet_deploy_user,
                        password: calnet_deploy_password
                )
            }
        }
    }
}
